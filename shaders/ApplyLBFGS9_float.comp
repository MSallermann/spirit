#version 450
#extension GL_KHR_shader_subgroup_arithmetic : enable

layout (local_size_x_id = 1) in;
layout (constant_id = 2) const int sumSubGroupSize = 64;

layout(binding = 0) buffer Data
{
   float s [];
};
layout(binding = 1) buffer Data0
{
   float s_copy [];
};
layout(binding = 2) buffer Data1
{
   float sd [];
};
layout(binding = 3) buffer Data2
{
   float torque0 [];
};
layout(binding = 4) buffer Data3
{
   float torque1 [];
};
layout(binding = 5) buffer Data4
{
   float energy [];
};


layout(push_constant) uniform PushConsts
{
  uint num_mem;
  uint nos;
  uint pad;
  float eps;
  uint k;
  float max_move;
} consts;
shared float sdata[sumSubGroupSize];

void main() {

	uint id_x=gl_GlobalInvocationID.x;

	//float s_norm=1.0;
	vec3 sd_loc;
	sd_loc.x=sd[id_x];
	sd_loc.y=sd[id_x+consts.pad];
	sd_loc.z=sd[id_x+2*consts.pad];
	float theta1 =length(sd_loc);
	float theta1_inv = 1.0/theta1;
	
	float f_der0=(torque0[id_x]*sd_loc.x+torque0[id_x+consts.pad]*sd_loc.y+torque0[id_x+2*consts.pad]*sd_loc.z);
	float f_der1=(torque1[id_x]*sd_loc.x+torque1[id_x+consts.pad]*sd_loc.y+torque1[id_x+2*consts.pad]*sd_loc.z);
	float c1 = (-2*(energy[gl_GlobalInvocationID.x]-energy[consts.nos+gl_GlobalInvocationID.x])*theta1_inv+f_der1+f_der0)*theta1_inv*theta1_inv;
	float c2 = (3*(energy[gl_GlobalInvocationID.x]-energy[consts.nos+gl_GlobalInvocationID.x])*theta1_inv-f_der1-2*f_der0)*theta1_inv;
	float c3 = f_der0;
	float c4 = energy[consts.nos+gl_GlobalInvocationID.x];
	float det=c2*c2-3*c1*c3;
	float alpha =0;
	if (det >0)
		alpha = (-c2+sqrt(det))/(3*c1);
	if ((alpha<=0) || (alpha >= theta1)){
		if (energy[gl_GlobalInvocationID.x]>energy[consts.nos+gl_GlobalInvocationID.x])
			alpha=0;
		else
			alpha=theta1;
			
	}
	/*if (energy[gl_GlobalInvocationID.x]>energy[consts.nos+gl_GlobalInvocationID.x])
		alpha=0;
	else
		alpha=theta1;*/
	sd_loc*=alpha*theta1_inv;
	sd[id_x]=sd_loc.x;
	sd[id_x+consts.pad]=sd_loc.y;
	sd[id_x+2*consts.pad]=sd_loc.z;
	
	if (alpha<=0){
		s[id_x]=s_copy[id_x]; 
		s[id_x+consts.pad]=	s_copy[id_x+consts.pad]; 
		s[id_x+2*consts.pad]= s_copy[id_x+2*consts.pad]; 
	} else{
		if ((alpha < theta1) && (alpha > 1.0e-20)){
			vec3 s_loc;
			s_loc.x=s_copy[id_x];
			s_loc.y=s_copy[id_x+consts.pad];
			s_loc.z=s_copy[id_x+2*consts.pad];
			float theta = alpha;
			float theta_inv = 1.0/theta;
			float q = cos(theta);
			float w = 1 - q;
			float x = -sd_loc.x* theta_inv;
			float y = -sd_loc.y* theta_inv;
			float z = -sd_loc.z* theta_inv;
			float s1 = x * y * w;
			float s2 = x * z * w;
			float s3 = z * y * w;
			float sin_theta=sin(theta);
			float p1 = z * sin_theta;
			float p2 = y * sin_theta;
			float p3 = x * sin_theta;
			vec3 t;
		
			t.x = (q + x* x * w) * s_loc.x + (s1 - p1) * s_loc.y + (s2 + p2) * s_loc.z;
			t.y = (s1 + p1) * s_loc.x + (q + y * y * w) * s_loc.y + (s3 - p3) * s_loc.z;
			t.z = (s2 - p2) * s_loc.x + (s3 + p3) * s_loc.y + (q + z * z * w) * s_loc.z;
			t=normalize(t);
			s[id_x] = t.x;
			s[id_x+consts.pad] = t.y;
			s[id_x+2*consts.pad] = t.z;
		};
	}
	

}